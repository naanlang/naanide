/*
 * nideClient.nlg
 * NaanIDE version 0.9.26+1
 *
 *     Naan IDE controller for browsers.
 *
 * column positioning:                          //                          //                      !
 *
 * Copyright (c) 2017-2024 by Richard C. Zulch
 *
 */


/*
 * NideClient
 *
 *     This runs in browsers to implement an IDE. It requires NideCommon and NideUI to operate. The
 * following are explanations of key Nide components and designs.
 *
 * Location
 *
 *     A Location is a named dictionary describing a place where project storage can take place.
 * Locations are referred to by name, which is a user-visible string.
 *
 * ProjectWhere
 *
 *     A ProjectWhere dictionary has the following structure:
 * {
 *     location: <string>   -- name of location containing project storage access information
 *     path: <string>       -- path of project within location
 *     name: <string>       -- name of project for user
 *     typeID: <string>     -- project type ID, which is decoded for the user
 * }
 *
 * The ProjectWhere structure is designed to have enough information to find the project
 * or explain to the user why it cannot be found. The name does not exist when the project is just
 * a location with no independent state, like a folder.
 *
 */


/*
 * niccPrefs
 *
 *     Preferences object for persistent preferences outside of projects. This also opens the Nide
 * database for local browser storage.
 *
 */

closure niccPrefs(nicc, dbname, local prefs, error, data) {
    global()
    `(error, data) = require("naanlib:frameworks/storage/dbt_pouch.nlg").PouchDB(dbname)
    if error {
        ErrorDebuglog("Can't access Nide local browser database", error)
        return (false)
    }
    nicc.db = data
    `(error, data) = nicc.db.table("NideData")
    if error {
        ErrorDebuglog("Can't access NideData table", error)
        return (false)
    }
    nicc.dbtable = data
    prefs = new(object, this)
    
    // save
    // Save preferences to pouchDB
    prefs.save = function save() {
        if prefs.doc {
            prefs.doc.content = prefs.data
            `(error, data) = nicc.dbtable.update(prefs.doc)
            prefs.doc = data }
        else {
            `(error, data) = nicc.dbtable.add("NidePreferences", prefs.data)
            prefs.doc = data }
        if error
            nicc.alert("<b>Error saving preferences</b><br>".concat(ErrorString(error)), { class: "alert-danger" })
    }

    // default
    // Load default preferences
    prefs.default = closure default(local error, config, extra) {
        `(error, config, extra) = nicc.api.readFile("nide_options.cfg", {
            encoding: "json"
        })
        if error {
            ErrorDebuglog("Can't read nide_options.cfg", error)
            config = { } }
        else
            config = new(config)
    }

    // load
    // Load preferences from pouchDB
    prefs.load = function load(local key) {
        `(error, data) = nicc.dbtable.get("NidePreferences")
        if error {                                                          // missing preferences or other error
            if error.status != 404
                ErrorDebuglog("Can't load preferences", error)
            prefs.data = { } }
        else {
            prefs.doc = data
            prefs.data = data.content
        }
        data = default()
        for key in data
            if !prefs.data[key]
                prefs.data[key] = data[key]                                 // bring in any new keys from configuration
    }
    prefs.load()
    
    // loadplugins
    // Load listed plugins
    prefs.loadplugins = function loadplugins(local folder, path, error, config, extra, name) {
        folder = "plugins/"
        path = folder.concat("plugins.cfg")
        `(error, config, extra) = nicc.api.readFile(path, {
            encoding: "json"
        })
        if error
            ErrorDebuglog("Can't read plugins.cfg", error)
        else
            prefs.plugins = new(config)
        for name in prefs.plugins.load {
            path = folder.concat(name)
            try {
                require(path)
            } catch {
                if true
                    ErrorDebuglog("Exception requiring: ", path, exception)
            }
        }
    }

    // finis
    prefs
};


/*
 * niccTermControl
 *
 *     Control the IDE REPL window, including saving and restoring, closing at quit, etc.
 *
 */

closure niccTermControl(local newin, termState, winOpen) {
    global(App, js, window)

    // getTS
    //
    function getTS(local wfeatures) {
        termState = js.t.LoadPref("nideTerm")
        if !termState
            termState = {
                height: 500
                width: 900
            }
        winOpen = termState.open
        wfeatures = strcat("height=", termState.height, ",width=", termState.width)
        if termState.screenX
            wfeatures = wfeatures.concat(",screenX=", termState.screenX, ",screenY=", termState.screenY)
        wfeatures
    }
    
    // putTS
    //
    function putTS() {
        js.t.SavePref("nideTerm", termState)
    }
    
    // saveTermWin
    //
    function saveTermWin() {                                                // save window size/position on close
        if newin {
            termState.screenX = newin.screenX
            termState.screenY = newin.screenY
            termState.height = newin.innerHeight+1
            termState.width = newin.innerWidth
            putTS()
        }
    }

    //
    // App.openTerm
    //
    // Open or reopen the IDE REPL terminal.
    //

    App.openTerm = function() {
        newin = window.open(window.location.origin.concat("/naan_term.html", requireQuery()), "naan_term", getTS())
        winOpen = true
        newin.addEventListener("beforeunload", function(e) {                // save terminal window size/position on close
            winOpen = false                                                 // mark as closed for future save
            saveTermWin()
            newin = false
        })
        
        window.addEventListener("beforeunload", function (e) {              // close terminal window when IDE closes
            termState.open = winOpen                                        // desired restart state
            saveTermWin()
            newin.close()
        })

        newin
    }
    
    //
    // initialize
    //
    
    getTS()
    if winOpen
        App.openTerm()                                                      // like last time
};


/*
 * niccController
 *
 *     Make an NaanIDE object. This does not perform any UI operations.
 *
 */

closure niccController(api, local nicc, result, homeDir, pollint, version) {
    global(App, js, document, window, nideTerminals)
    nicc = new(object, this)
    nicc.alert = closure prealert(text, options) {
        future(function() { nicc.alert(text, options) }, 1000)              // reschedule alert if too early
    }
    App.nide = nicc
    nicc.statusEl = document.getElementById("NideStatus")
    nicc.hostpath = window.location.origin.concat(js.r("path").dirname(window.location.pathname))
    if nicc.hostpath.slice(-1) != "/"
        nicc.hostpath = nicc.hostpath.concat("/")                           // must end in "/"
    nicc.baseurl = window.location.href.split(RegExp("[?#]"))[0]            // URL without variables or hashes
    nicc.api = api
    nicc.prefs = niccPrefs(nicc, "Nide")
    nicc.uilist = []
    nicc.psm = require("naanlib:frameworks/client/psm_client.nlg").MakeClientPSM(api, "Nide")
    nicc.locate = nicc.psm.tracker()                                        // storage locations
    nicc.locate.open()
    nicc.track = ExecutorTracker()
    nicc.track.register(localWorker, "Local")
    nicc.track.register(hostWorker, "Host")
    nicc.projman = require("naanlib:frameworks/project/projects.nlg").ProjectManager(nicc.locate, nicc.track, "Nide").1
    nicc.projects = nicc.projman.tracker()
    nicc.projects.open()
    nicc.sworker = require("naanlib:frameworks/browser/workers.nlg").TrackServiceWorkers(nicc.track, "NaanIDE", "0.9.26+1", function() {
        nicc.upgrade_sw = true
        nicc.checker.run(1)                                                 // run the update checker very soon
    })
    result = nicc.locate.ensureExists("PouchDB", {
        label: "Local"
        urlName: "NideProjects"
        existing: true
        locked: true })
    if result.0
        ErrorDebuglog("result", result.0, result.0.*)
    nicc.deflocation = nicc.getPrefs("DefLocName")
    if !nicc.deflocation {
        homeDir = nicc.locate.connect("Host", "NideFS", "").1.folderPath("HomeDir").1
        if !homeDir
            homeDir = ""
        setDefault("NodeFS", "Host", homeDir)
    }

    // localWorker
    //
    // Instantiate a worker in our local browser.
    //
    function localWorker(track, name, options) {
        options = merge({
            startup: {
                initcmds: 'App.version = "0.9.26+1";;\n'
                    'App.cache = "215b454e23eb959ac7b87e0bd400aa72";;\n'
                    'Naan.module.requireQuery({ naanver: App.cache });;\n'
                    'Naan.module.defineExt("nlg", true);;\n'
                    'if !module.owner.list.running.components.debugnub {\n'
                    '   require("naanlib:frameworks/common/repltools.nlg").ImportReplTools()\n'
                    '   require("naanlib:frameworks/running/debugnub.nlg").DebugNub()\n'
                    '   require("naanlib:frameworks/running/taskexec.nlg").TaskExecutor()\n'
                    '};;\n'
            }
        }, options)
        nideTerminals.TermLocal(track, name, options)
    }

    // hostWorker
    //
    // Instantiate a worker in our host server.
    //
    function hostWorker(track, name, options) {
        nideTerminals.TermHost(track, nicc.api, name, options)
    }

    // registerRemote
    //
    // Register a remote server with our execution tracker so we can connect a terminal to it. This
    // returns a remoteID for spawning terminals on this remote server. After registration, create a
    // debug terminal using a call like the following:
    //
    //  nicc.track.spawn(remoteID,              // as returned by registerRemote
    //          "NaanIDE Remote", {             // display name of terminal
    //      workerID: "NideServer"              // for main thread; other strings for worker threads
    //      type: "My Remotes"                  // display name of terminal group; e.g. URL
    //  })
    //
    nicc.registerRemote = closure registerRemote(api) {

        // called by nicc.track.spawn
        function remoteWorker(track, name, options) {
            nideTerminals.TermHost(track, api, name, options)
        }

        nicc.track.register(remoteWorker, api.url)                          // harmless to re-register
        api.url
    }

    // nicc.getPrefs
    // nicc.putPrefs
    //
    // Get and put preferences through functions so that changes can be tracked for debugging.
    //
    nicc.getPrefs = function getPrefs(key) {
        nicc.prefs.data[key]
    }

    nicc.putPrefs = function putPrefs(key, data) {
        nicc.prefs.data[key] = data
        nicc.prefs.save()
    }
        
    // nicc.openTerm
    //
    // Open the separate IDE terminal window, using the saved state if we have it.
    //
    nicc.openTerm = closure openTerm(local newin, termState, wfeatures) {
        
        // getTS
        //
        function getTS() {
            termState = js.t.LoadPref("nideTerm")
            if !termState
                termState = {
                    height: 500
                    width: 900
                }
        }
        
        // putTS
        //
        function putTS() {
            js.t.SavePref("nideTerm", termState)
        }
        
        // saveTermWin
        //
        function saveTermWin() {                                            // save window size/position on close
            termState.screenX = newin.screenX
            termState.screenY = newin.screenY
            termState.height = newin.innerHeight+1
            termState.width = newin.innerWidth
            termState.open = !newin.closed
            putTS()
        }

        getTS()
        wfeatures = strcat("height=", termState.height, ",width=", termState.width)
        if termState.screenX
            wfeatures = wfeatures.concat(",screenX=", termState.screenX, ",screenY=", termState.screenY)
        newin = window.open(window.location.origin.concat("/naan_term.html", requireQuery()), "naan_term", wfeatures)
        newin.addEventListener("beforeunload", saveTermWin.proc)            // save window size/position on close
        window.addEventListener("beforeunload", function (e) {              // close terminal window when IDE closes
            newin.close()
        })
        window.addEventListener("NaanSave", function (e) {
            window.naanlang.SavePref()
            saveTermWin()
        })
    }
     
    // appUpdateCheck
    //
    // Get the latest app version from the server, with a random query to defeat cacheing, and check
    // if our app needs to be updated. The result of this function is true iff we need to update.
    //
    nicc.appUpdateCheck = function appUpdateCheck(local url, error, data, result, version, age) {
        url = nicc.hostpath.concat(EncodeQuery("env_web.js?", {
            rnv: UUID()
        }))
        `(error, data) = await(window.fetch(url, { }))
        if !error
            `(error, data) = await(data.text())
        if error
            return (false)                                                  // errors tell us nothing
        result = RegExp('App.version = "([^"]+)"[\\s\\S]+App.cache = "([^"]+)"').exec(data)
        if !result.1 || !result.2 {
            debuglog("appUpdateCheck: can't get current version")
            window.localStorage.removeItem("nide-loading")
            return (false)
        }
        nicc.upgrade_nextversion = result.1
        age = VersionCheck(App.version, result.1)                           // result.1 is version; result.2 is cache
        if !age || age < 0 {                                                // <0 means we are older than server
            window.localStorage.setItem("nide-loading", "${result.1}|<font color=#00aa33>upgrading")
            true
        } else {
            window.localStorage.removeItem("nide-loading")
            false
        }
    }
    
    // appUpdateNow
    //
    // Update the app now by telling the service worker to reload.
    //
    nicc.appUpdateNow = function appUpdateNow() {
        appUpdateCheck()                                                    // ensure updating message set
        future(function() {
            nicc.sworker.serv.reload()
        }, 1)
    }
    
    // update at startup
    //
    future(function() {                                                     // update automatically at startup
        if nicc.upgrade_sw || appUpdateCheck()
            appUpdateNow()
        else
            niccTermControl()
    },10)
    nicc.checker = future(function() {                                      // check hourly - 3,600,000 msec
        if nicc.upgrade_sw || appUpdateCheck() {
            nicc.upalert = nicc.alert("<b>NaanIDE Upgrade Available</b><br>please reload page for version ".concat(nicc.upgrade_nextversion),
                { class: "alert-primary", unique: "app-upgrade" })
        }
        nicc.checker.run(3600000)
    }, 3600000)

    // status polling
    //
    //     This uses long polling to the API to receive server-initiated messages and detect
    // disconnection.
    //
    pollint = 60000                                                         // default timeout 1 minute
    version = "NaanIDE 0.9.26+1"
    function statusPoll(timeoutms) {
        api.status(function (status) {
            if not status {
                nicc.connected = false
                nicc.statusEl.innerHTML = version.concat("&nbsp;&nbsp;<font color=#aa3300>disconnected")
                future(function poll1(){statusPoll(1000)}, 1)               // once every second
            }
            else {
                if !nicc.connected
                    nicc.hostTerm.PostMessage("reconnected")                // make sure server knows about us
                nicc.connected = true
                if status.length > 0
                    nicc.statusEl.innerHTML = status[0].message
                else
                    nicc.statusEl.innerHTML = version.concat("&nbsp;&nbsp;<font color=#00aa33>ready")
                future(function poll2(){statusPoll(pollint)}, 300)          // try to reconnect more quickly
            }
            window.nideHost.setConnectivity(nicc.connected)
        }, timeoutms)
    }

    // setUI
    //
    // Ensure we can get to the UI, e.g. for alerts.
    //
    nicc.setUI = function setUI(niui) {
        App.ui = niui
        nicc.ui = niui
        nicc.alert = niui.alert
        if !nicc.prefs
            nicc.alert("<b>Local database failure</b><br>operations are severely curtailed", { class: "alert-danger" })
        else {
            while nicc.uilist.length > 0
                call(nicc.uilist.pop(), niui)                               // call pending UI plugins
            nicc.uilist = false }
    }
    
    // getDefLocation
    //
    // Get the default location.
    //
    nicc.getDefLocation = function getDefLocation() {
        nicc.deflocation
    }

    // setDefault
    //
    // Set the default location and optional path.
    //
    nicc.setDefault = function setDefault(classID, resID, path) {
        nicc.deflocation = {
            classID: classID
            resID: resID
            path: path
        }
        nicc.putPrefs("DefLocName", nicc.deflocation)
    }
    
    // addUI
    //
    // Add procedure to be called when UI is ready.
    //
    nicc.addUI = function addUI(proc) {
        if nicc.uilist
            nicc.uilist.push(proc)
        else
            proc(nicc.ui)
    }
    
    //
    // load plugins
    //
    
    nicc.prefs.loadplugins()
    addUI(function(local loading) {
        loading = window.localStorage.getItem("nide-loading")
        loading = loading.split("|")
        if loading.0 == "0.9.26+1"
            window.localStorage.removeItem("nide-loading")
        statusPoll(1)
    })

    // finis

    nicc
};


/*
 * niccInit
 *
 *     Initialize the component.
 *
 */

function niccInit(local manifest, nideRunning) {
    manifest = `(niccPrefs, niccTermControl, niccController, niccInit)

    Naan.module.build(module.id, "nideClient", function(modobj, compobj) {
        compobj.manifest = manifest
        modobj.exports.MakeNideController = niccController
        function niccReload() {
            jspath = js.r("path")
            window = js.w
            document = window.document
        } ()
        module.reload = niccReload
    })

    App.model = require("model.nlg").NideModel()
    require("../common").LiveImport()
    require("naanlib:frameworks/storage/dbt_pouch.nlg")
    require("naanlib:frameworks/project/projects.nlg")
    nideRunning = require("naanlib:frameworks/running/executors.nlg")
    nideTerminals = require("naanlib:frameworks/browser/terminals.nlg")
    Naan.module.letimport(nideRunning.ExecutorTracker)
    Naan.module.letimport(nideRunning.ExecutorBase)
    require("naanlib:frameworks/client/psm_client.nlg")
} ();
