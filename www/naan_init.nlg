/*
 * naan_init.nlg
 * Naanide
 *
 * Loaded automatically to initialize the main Naanide browser thread.
 *
 * column positioning:                          //                          //                      !
 *
 * Copyright (c) 2017-2023 by Richard C. Zulch
 *
 */

loglevel(3);
chns("Start");

defineExt("nlg", true);                                                     // our code extension
require("naan_init_common.nlg");                                            // initialization for all Naan instances


/*
 * loadSecret
 * storeSecret
 *
 *     We have to track the secret API-access guid separately because the main persistence is
 * version-dependent, and if we reload we might be changing versions.
 *
 */

function loadSecret() {
    try {
        js.w.localStorage.getItem("Nide_Guid")
    } catch {
        if true { false }
    }
};

function storeSecret(guid) {
    try {
        js.w.localStorage.removeItem("Nide_Guid")
        js.w.localStorage.setItem("Nide_Guid", guid)
    } catch {
        if true { false }
    }
};


/*
 * loadNide
 *
 *     Load Naanide into the browser, whether for the first time or after reload.
 *
 */

serverurl = js.w.location.protocol.concat("//", js.w.location.host);
authState = { };

function loadNide(local hostpath, baseurl, guid, areq, bapi, bide, bui) {
    hostpath = js.w.location.origin.concat(js.r("path").dirname(js.w.location.pathname))
    if hostpath.slice(-1) != "/"
        hostpath = hostpath.concat("/")                                     // must end in "/"
    baseurl = js.w.location.href.split(RegExp("[?#]"))[0]                   // URL without variables or hashes
    guid = xnew(js.w.URL, js.w.location.href).searchParams.get("guid")
    if guid {                                                               // remove the GUID from the visible URL
        storeSecret(guid)
        authState.guid = guid
        js.w.history.replaceState(authState, "", baseurl)
    }
    else
        guid = loadSecret()                                                 // see if we have stored the GUID
    areq = require("frameworks/browser/https_request.nlg").HttpsApiRequest
    bapi = require("frameworks/client/apiclient.nlg").NideAPIclient(hostpath, guid, areq)
    bide = require("nide/nideClient/nideClient.nlg").MakeNideController(bapi)
    bui = require("nide/nideUI/nideUI.nlg").MakeNideUI(bide)
    bide.setUI(bui)
} ();

module.postload = loadNide;                                                 // called again after reload
